Из журнала ZX-Guide#1, Рязань, 28.11.1998
(фрагмент раздела 'Этюды')



 Alone Coder

   <Точка Старых>: по координатам,заданным
в регистрах L=y, E=x, в произвольном месте
экрана(и даже за экран)за 58(!)тактов ста-
вится точка.Регистр C равен старшему байту
адреса таблиц('TABLE), регистр D=C+2.
     LD H,C
     LD A,(DE);x/8 \
     INC D         |-даёт младший байт
     OR (HL);L(y)  /
     INC H
     LD H,(HL);H(y)
     LD L,A
     LD A,(DE);byte(x)
    (X)OR (HL);метод постановки точки
     LD (HL),A
   Если вместо последних 2 команд вставить
AND (HL),то это будет функция POINT. Чтобы
процедура работала многократно, в конце её
поставьте DEC D. Таблицы генерируются сле-
дующей программой:
     LD HL,TABLE+256
     LD DE,#4000
GEN0 LD (HL),D  \
     DEC H      |
     LD (HL),E  |
     INC H      |
     DOWN DE    | 1-я
     LD A,D     |- и
     SUB 88     | 2-я
     JR NZ,$+3  |
     LD D,A     |
     INC L      |
     JR NZ,GEN0 /
     INC H
     LD A,128
GEN1 LD (HL),E  \
     INC H      |
     LD (HL),A  |
     DEC H      | 3-я
     RRCA       |- и
     JR NC,$+3  | 4-я
     INC E      |
     INC L      |
     JR NZ,GEN1 /